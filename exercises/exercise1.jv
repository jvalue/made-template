// This Jayvee model describes a pipeline from a CSV file in the web
// to a SQLite file sink concerning airport data.
pipeline AirportPipeline {

    AirportExtractor
        -> AirportTextFileInterpreter
        -> AirportCSVInterpreter
        -> AirportTableInterpreter
        -> AirportsLoader;

    block AirportExtractor oftype HttpExtractor {
        url: "https://opendata.rhein-kreis-neuss.de/api/explore/v2.1/catalog/datasets/rhein-kreis-neuss-flughafen-weltweit/exports/csv?lang=en&timezone=Europe%2FBerlin&use_labels=true&delimiter=%3B";
    }

    block AirportTextFileInterpreter oftype TextFileInterpreter { }

    block AirportCSVInterpreter oftype CSVInterpreter {
        enclosing: '';
        delimiter: ";";
    }

    block AirportTableInterpreter oftype TableInterpreter {
        header: true;
        columns: [
            "Lfd. Nummer" oftype integer,
            "Name des Flughafens" oftype text,
            "Ort" oftype text,
            "Land" oftype text,
            "IATA" oftype text,
            "ICAO" oftype text,
            "Latitude" oftype decimal,
            "Longitude" oftype decimal,
            "Altitude" oftype integer,
            "Zeitzone" oftype decimal,
            "DST" oftype text,
            "Zeitzonen-Datenbank" oftype text,
            "geo_punkt" oftype text
        ];
    }

    block AirportsLoader oftype SQLiteLoader {
        table: "airports";
        file: "./airports.sqlite";
    }

}